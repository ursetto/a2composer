10  REM  ***************************
20  REM  *        COMPOS<CTRL-E>R         *
30  REM  *     BY JIM URSETTO      *
40  REM  *   USE WITH MIDI-MAGIC   *
50  REM  *  INTERFACE AND PROGRAM  *
60  REM  ***************************
70  IF  PEEK(104) = 64  THEN 96
80  POKE 104,64: POKE 16384,0
95  PRINT  CHR$(4);"RUN COMPOSER4"
96  PRINT  CHR$(4);"BLOAD MIDI-MAGIC"
97  POKE 2459,80: POKE 2455,0: POKE 20415,0: POKE 20414,0: POKE 20413,0
100 MEASURE = 1:NLOCTE = 16448:LNGTH = 0:NT$ = "":CNFIRM = 0:BT = 1:NTNUM = 1:TEMPO = 16440:TM = 72: POKE TEMPO,TM
105 FL$ = "<None>        "
110  HOME 
114  REM 
115  REM   DISPLAY HEADER
116  REM 
120 MG$ = "***************************": VTAB 1: GOSUB 5000
130 MG$ = "*        COMPOSER         *": GOSUB 5000
140 MG$ = "*     BY JIM URSETTO      *": GOSUB 5000
150 MG$ = "***************************": GOSUB 5000
160  POKE 34,4
164  REM 
165  REM  MAIN MENU
166  REM 
167  ONERR  GOTO 5002
170  VTAB 1: HTAB 58: PRINT "File: "FL$
175  VTAB 1: HTAB 1: PRINT  SPC( 7);"Main Menu"
180  VTAB 7: PRINT  TAB( 28)"1. Begin a new song"
190  PRINT  TAB( 28)"2. Continue a song in memory"
200  PRINT  TAB( 28)"3. Get a song on disk"
210  PRINT  TAB( 28)"4. Save a song to disk"
220  PRINT  TAB( 28)"5. Reset tempo to normal"
225  PRINT  TAB( 28)"6. Play a song"
227  PRINT  TAB( 28)"7. Quit"
230  VTAB 16: HTAB 34: PRINT "Select: ";
240  GET CH
250  IF CH <1  OR CH >8  THEN  GOTO 240
260  PRINT CH;
265  IF CH = 8  THEN  GOTO 1500
270  IF CH = 1  THEN  GOTO 300
275  IF CH = 2  THEN  GOTO 700
280  IF CH = 3  THEN  GOTO 800
285  IF CH = 4  THEN  GOTO 900
290  IF CH = 5  THEN  GOTO 1000
295  IF CH = 6  THEN  GOTO 1100
296  IF CH = 7  THEN  END 
297  REM  
298  REM  BEGIN A SONG
299  REM 
300  HOME 
301  ONERR  GOTO 4999
310  VTAB 1: HTAB 2: PRINT "Begin a song          "
320  VTAB 7:MG$ = "--- Status ---": GOSUB 5000
325  POKE 20480,255: POKE 20481,10: POKE 20482,0
327 NC = 3
328 NTNUM = 1
330  VTAB 9: PRINT "NOTE ";NTNUM
340  VTAB 15: INPUT " ENTER NOTE: ";NT$
345  IF NT$ = "ENDSONG"  THEN  POKE 20480 +NC,255: POKE 20481 +NC,255: VTAB 15: PRINT  SPC( 25);: GOTO 167
350  IF  LEFT$(NT$,2) = "CA"  THEN 600
351  IF NT$ = "REST"  THEN 500
352  IF NT$ = "MENU"  THEN  VTAB 15: PRINT  SPC( 25);: GOTO 167
353 C =  LEN(NT$):A$ =  LEFT$(NT$,C -1):B$ =  RIGHT$(NT$,1):B =  VAL(B$)
354  IF A$ = "C"  THEN BC = 36 +12 *B: GOTO 369
355  IF A$ = "C#"  THEN BC = 37 +12 *B: GOTO 369
356  IF A$ = "D"  THEN BC = 38 +12 *B: GOTO 369
357  IF A$ = "D#"  THEN BC = 39 +12 *B: GOTO 369
358  IF A$ = "E"  THEN BC = 40 +12 *B: GOTO 369
359  IF A$ = "F"  THEN BC = 41 +12 *B: GOTO 369
360  IF A$ = "F#"  THEN BC = 42 +12 *B: GOTO 369
361  IF A$ = "G"  THEN BC = 43 +12 *B: GOTO 369
362  IF A$ = "G#"  THEN BC = 44 +12 *B: GOTO 369
363  IF A$ = "A"  THEN BC = 45 +12 *B: GOTO 369
364  IF A$ = "A#"  THEN BC = 46 +12 *B: GOTO 369
365  IF A$ = "B"  THEN BC = 47 +12 *B: GOTO 369
369  IF BC <36  OR BC >96  THEN  GOTO 330
370  POKE 20480 +NC,BC
380 NT = NT +1:NC = NC +1
390  GOTO 330
500  VTAB 17: INPUT "DURATION: ";DR
510  POKE 20480 +NC,255:TP = DR/.5 *10: POKE 20481 +NC,TP
515 NC = NC +2: VTAB 17
520  GOTO 330
600  HTAB 1: VTAB 17: INPUT "STOP WHICH NOTE? ";ST$
653 C =  LEN(ST$):A$ =  LEFT$(ST$,C -1):B$ =  RIGHT$(ST$,1):B =  VAL(B$)
654  IF A$ = "C"  THEN BC = 36 +12 *B: GOTO 669
655  IF A$ = "C#"  THEN BC = 37 +12 *B: GOTO 669
656  IF A$ = "D"  THEN BC = 38 +12 *B: GOTO 669
657  IF A$ = "D#"  THEN BC = 39 +12 *B: GOTO 669
658  IF A$ = "E"  THEN BC = 40 +12 *B: GOTO 669
659  IF A$ = "F"  THEN BC = 41 +12 *B: GOTO 669
660  IF A$ = "F#"  THEN BC = 42 +12 *B: GOTO 669
661  IF A$ = "G"  THEN BC = 43 +12 *B: GOTO 669
662  IF A$ = "G#"  THEN BC = 44 +12 *B: GOTO 669
663  IF A$ = "A"  THEN BC = 45 +12 *B: GOTO 669
664  IF A$ = "A#"  THEN BC = 46 +12 *B: GOTO 669
665  IF A$ = "B"  THEN BC = 47 +12 *B: GOTO 669
669  IF BC <36  OR BC >96  THEN  GOTO 600
670 BC = BC +128: POKE 20480 +NC,BC:NC = NC +1: GOTO 330
700  HOME : ONERR  GOTO 4999
710  VTAB 1: HTAB 2: PRINT "Continue song        "
720  VTAB 7:MG$ = "--- Status ---": GOSUB 5000
730  GOTO 330
800  HOME : ONERR  GOTO 6000
810  VTAB 1: HTAB 1: PRINT "Get a song...        "
820  VTAB 11: HTAB 22: INPUT "File to load: ";FI$
830  VTAB 12: HTAB 22: INPUT "Info File: ";IN$
840  IF FI$ = ""  AND IN$ = ""  THEN 167
850  IF FI$ = ""  THEN  GOTO 870
860  PRINT  CHR$(4);"BLOAD "FI$",A$5000"
865  IF IN$ = ""  THEN  GOTO 879
870  PRINT  CHR$(4);"OPEN "IN$
872  PRINT  CHR$(4);"READ "IN$
874  INPUT NT: INPUT NC: INPUT TM
876  PRINT  CHR$(4);"CLOSE "IN$
878  POKE TEMPO,TM: IF FI$ = ""  THEN  GOTO 167
879 FL$ = FI$
880  GOTO 167
900  HOME : ONERR  GOTO 6001
910  VTAB 1: HTAB 1: PRINT "Save a song...         "
920  VTAB 11: HTAB 22: INPUT "File to Save: ";FS$
930  VTAB 12: HTAB 22: INPUT "Info File: ";IS$
940  IF FS$ = ""  AND IS$ = ""  THEN  GOTO 167
950  IF FS$ = ""  THEN  GOTO 967
960  PRINT  CHR$(4);"BSAVE "FS$",A$5000,L"NC +5
965  IF IS$ = ""  THEN  GOTO 979
967  PRINT  CHR$(4);"OPEN "IS$
968  PRINT  CHR$(4);"DELETE "IS$
970  PRINT  CHR$(4);"OPEN "IS$
972  PRINT  CHR$(4);"WRITE "IS$
974  PRINT NT: PRINT NC: PRINT TM
976  PRINT  CHR$(4);"CLOSE "IS$
978  IF FS$ = ""  THEN 167
979 FL$ = FI$
980  GOTO 167
1000  HOME : VTAB 1: HTAB 1: PRINT "Reset tempo       "
1010  VTAB 11: HTAB 28: PRINT "Resetting tempo...";: POKE TEMPO,72:TM = 72: PRINT " Done!": FOR I = 1 TO 500: NEXT I: GOTO 167
1100  HOME : VTAB 1: HTAB 1: PRINT "Play a song"
1110  VTAB 11: HTAB 35: PRINT "Playing...";
1120  CALL 2429: CALL 2614
1130  PRINT " Done!";: GOTO 167
1500  HOME : VTAB 1: HTAB 1: PRINT "Adjust tempo"
1510  VTAB 11: HTAB 30: PRINT "Tempo: ";TM;"  ";
1520  POKE  -16368,0
1530 K =  PEEK( -16384)
1535  POKE  -16368,0
1540  IF K = 149  THEN TM = TM +1: GOTO 1510
1550  IF K = 136  THEN TM = TM -1: GOTO 1510
1560  IF K = 138  THEN TM = 0: GOTO 1510
1567  IF K = 139  THEN TM = 255: GOTO 1510
1570  IF K = 141  THEN  POKE TEMPO,TM: GOTO 167
1580  IF K = 160  THEN  POKE TEMPO,TM: GOTO 167
1590  GOTO 1530
4998  POKE 34,0
4999  END 
5000  HTAB 40 - LEN(MG$)/2: PRINT MG$: RETURN 
5002  GOTO 240
6000  VTAB 17: HTAB 22: PRINT "Disk Error. Press a key to continue.";: POKE 216,0: GET DE$: GOTO 800
6001  VTAB 17: HTAB 22: PRINT "Disk Error. Press a key to continue.";: POKE 216,0: GET DE$: GOTO 900